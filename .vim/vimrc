" ~/.vim/vimrc

language messages C

runtime bundle/pathogen/autoload/pathogen.vim
silent! execute pathogen#infect()

let $VIMFILES = expand('<sfile>:h')

if empty(&termencoding)
  let &termencoding = exists('$MSYSTEM') ? 'cp1251' : &encoding
endif
set encoding=utf-8
set fileencodings=ucs-bom,utf-8,cp1251

set cpoptions-=a cpoptions-=A
set pastetoggle=<F2>

set incsearch
set ignorecase smartcase

set tags=./tags;,tags

set scrolloff=1
if has('patch-7.4.338')
  set breakindent breakindentopt=sbr
endif
set showbreak=+
set sidescrolloff=5
set display=lastline
set cmdheight=2
set listchars=eol:$,tab:>-,trail:-,extends:>,precedes:<,nbsp:%

set background=dark
set hlsearch
set spelllang=en_us,ru_yo

set laststatus=2
set statusline=%n\ %<%.20f\ %h%m%r%{mine#lines#GetFlags()}%=%-14.(%l,%c%V%)\ %P

set tabline=%!mine#lines#GetTabLine()

set notitle

set visualbell t_vb=

set undofile
let &undodir = $VIMFILES . '/undo'
set backspace=indent,eol,start
if has('patch-7.3.541')
  set formatoptions+=j
endif
set complete-=i complete-=t
set completeopt-=preview
set infercase
set nrformats-=octal

set shiftwidth=4 softtabstop=4
set expandtab
set autoindent

set timeoutlen=3000
set ttimeoutlen=100

set modeline
if has('patch-7.4.785')
  set nofixendofline
endif
set autowrite
set autoread

set history=200
set wildmode=longest:full,full
set wildmenu

if executable('ack')
  set grepprg=ack\ -H\ --column grepformat=%f:%l:%c:%m
endif

set keymap=russian
set iminsert=0 imsearch=-1

set virtualedit=block
set sessionoptions-=blank sessionoptions-=options
set viminfo=!,'20,<50,s10,h

filetype plugin indent on
syntax on

colorscheme mine

call mine#lines#ResetFlags()
call mine#lines#AddFlags(['fugitive#statusline', 'SyntasticStatuslineFlag'])

let g:ale_lint_on_text_changed = 'normal'
let g:ale_lint_on_insert_leave = 1
let g:ale_linters = {'erlang': ['syntaxerl']}

let g:user_emmet_leader_key = '<C-\>'

let g:netrw_bufsettings = 'noma nomod nonu nobl nowrap ro rnu'
let g:netrw_banner = 0
let g:netrw_list_hide = '\v\C%(\~|\.swp)\*?$'
let g:netrw_timefmt = '%a %d %b %Y %H:%M:%S'

let g:sneak#s_next = 1
let g:sneak#map_netrw = 0

let g:surround_56 = "/* \r */"

let g:syntastic_always_populate_loc_list = 1
let g:syntastic_check_on_wq = 0
let g:syntastic_stl_format = '[!]'

let g:tern#command = ['tern', '--no-port-file']
let g:tern_show_signature_in_pum = 1

let g:vdebug_options = {}
let g:vdebug_options.watch_window_style = 'compact'
let g:vdebug_options.marker_default = '*'
let g:vdebug_options.marker_closed_tree = '+'
let g:vdebug_options.marker_open_tree = '-'
let g:vdebug_options.continuous_mode = 1

let g:go_highlight_trailing_whitespace_error = 0
if executable('goimports')
  let g:gofmt_command = 'goimports'
endif

let g:html_no_rendering = 1
let g:html_indent_autotags = 'html'
let g:html_indent_script1 = 'auto'
let g:html_indent_style1 = 'auto'

let g:perl_include_pod = 0

if has('digraphs')
  " HORIZONTAL ELLIPSIS.
  digraphs ,. 8230
endif

cnoremap <C-X>n <Down>
cnoremap <C-X>p <Up>

inoremap <C-U> <C-G>u<C-U>

nnoremap + <C-W>+
nnoremap _ <C-W>-

nnoremap <silent> - :Explore<CR>

nnoremap  <BS> <C-W>w
nnoremap g<BS> <C-W>W

nnoremap <C-J> gT
nnoremap <C-K> gt

nnoremap ymm :mksession! <C-R>=fnameescape(v:this_session)<CR><CR>

nnoremap <silent> <C-L> :nohlsearch<CR><C-L>

nnoremap Q @q

nnoremap gK :grep -w <C-R>=shellescape(expand('<cword>'))<CR><CR>

nnoremap gY :tselect<Space>
nnoremap gy :tag<Space>

nnoremap <expr> j v:count ? 'j' : 'gj'
nnoremap <expr> k v:count ? 'k' : 'gk'

noremap! <F3> <C-^>

runtime macros/matchit.vim

if exists('#fedora')
  autocmd! fedora BufReadPost *
endif

let s:localrc = $VIMFILES . '/vimrc.local'
if filereadable(s:localrc)
  let $MYLOCALVIMRC = s:localrc
  execute 'source' fnameescape(s:localrc)
endif
unlet s:localrc
