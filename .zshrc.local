#
# ~/.zshrc.local
#
# This file is read at the end of Grml's zshrc
#
# <https://git.grml.org/f/grml-etc-core/etc/zsh/zshrc>
#

setopt CSH_NULL_GLOB

HISTFILE=$ZSH_STATE_HOME/history

export EDITOR=vim PAGER=less
READNULLCMD=$PAGER

export BLOCK_SIZE=human-readable

export ERL_AFLAGS='+pc unicode -kernel shell_history enabled'

export KERL_BUILD_DOCS=yes
export KERL_DOC_TARGETS='man chunks'

export LESS=eFiQRX
export LESSHISTFILE=-

export MANOPT='--no-hyphenation --no-justification'
export MANWIDTH=80

export PERLDOC=-oman

path=(
    $HOME/.local/bin
    $HOME/bin
    $HOME/.local/opt/universal-ctags/bin
    $HOME/.local/opt/mit-scheme/bin
    $path
)

cdpath=(.. $HOME $HOME/src/*(/N))

alias -g C='| wc -l'
alias -g G='| grep -Ei'
alias -g GE='|& grep -Ei'
alias -g J='| jql'
alias -g L='| less'
alias -g LE='|& less'
alias -g N='>&/dev/null'
alias -g NE='2>/dev/null'
alias -g V='| vim -'
alias -g X='| xargs'

alias ,='cd ..' -='cd -'
alias d='dirs -v'
alias h='history -d'
alias j=jobs
alias po=popd
alias pu=pushd
alias type='type -a'

alias bc='bc -lq'
alias crawl='telnet crawl.develz.org 345'
alias free='free -hl'
alias git='noglob git'
alias info='info --vi-keys'
alias mkdir='mkdir -p'
alias mmv='noglob zmv -W'
alias psu='ps -o pid,user,tty,etime,args -U $USERNAME'
alias topu='top -U $USERNAME'

unhash -a vim 2>/dev/null
unhash -f vim 2>/dev/null
alias vi=vim

HELP_ZLE_CACHE_FILE=$ZSH_CACHE_HOME/help-zle
ZLE_SPACE_SUFFIX_CHARS='&|'

bindkey '\eq' push-line-or-edit

bindkey '^XN' history-beginning-search-forward-end
bindkey '^XP' history-beginning-search-backward-end

bindkey '^Xn' history-beginning-search-forward
bindkey '^Xp' history-beginning-search-backward

bindkey -M menuselect h vi-backward-char
bindkey -M menuselect j vi-down-line-or-history
bindkey -M menuselect k vi-up-line-or-history
bindkey -M menuselect l vi-forward-char

function __prompt_remote_host {
    if (( ! ${+STY} )) REPLY=${SSH_CONNECTION+%m }
}

grml_theme_add_token cwd '%1~ ' '%B%F{blue}' %f%b
grml_theme_add_token remote-host -i __prompt_remote_host

zstyle :prompt:grml:left:setup items rc remote-host cwd vcs percent
zstyle :prompt:grml:right:setup use-rprompt false

zstyle ':vcs_info:*' enable git hg svn

zstyle ':vcs_info:*' actionformats \
    '%F{magenta}[%F{green}%b%F{yellow}|%F{red}%a%F{magenta}]%f ' %s:%r

zstyle ':vcs_info:*' formats \
    '%F{magenta}[%F{green}%b%F{magenta}]%f ' %s:%r

for rc ($ZSH_CONFIG_HOME/rc.d/*.zsh(.N)) source $rc
unset rc
