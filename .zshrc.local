#
# ~/.zshrc.local
#

setopt cshnullglob

READNULLCMD=less

export BLOCK_SIZE=human-readable

export COMPOSER_HOME=$HOME/.composer

export GOPATH=$HOME/go

export GPG_TTY=$TTY

export LESS=-iMQR
export LESSHISTFILE=-

export MANOPT='--no-hyphenation --no-justification'
export MANWIDTH=80

export PERLDOC=-oman

if [[ -x $HOME/.nodenv/bin/nodenv ]]; then
    path=($HOME/.nodenv/bin $path)
    eval "$(nodenv init - --no-rehash zsh)"
fi

if [[ -x $HOME/.plenv/bin/plenv ]]; then
    path=($HOME/.plenv/bin $path)
    eval "$(plenv init - --no-rehash zsh)"
fi

path=(
    $HOME/bin
    $HOME/.local/bin
    $COMPOSER_HOME/vendor/bin
    $GOPATH/bin
    $path
)

cdpath=(.. $HOME $HOME/src)

alias -g C='| wc -l'
alias -g G='| grep -Ei'
alias -g GE='|& grep -Ei'
alias -g J='| python -m json.tool'
alias -g L='| less'
alias -g LE='|& less'
alias -g N='>&/dev/null'
alias -g NE='2>/dev/null'
alias -g V='| vim -'
alias -g X='| xargs'

alias ,='cd ..'
alias d='dirs -v'
alias h='history -d'
alias j=jobs
alias po=popd
alias pu=pushd
alias type='type -a'

for n ({1..9}) alias $n="cd +$n"; unset n

alias bc='bc -lq'
alias crawl='telnet crawl.develz.org 345'
alias free='free -hl'
alias git='noglob git'
alias info='info --vi-keys'
alias men='erl -man'
alias mkdir='mkdir -p'
alias psu='ps -o pid,user,tty,etime,args -U $USERNAME'
alias topu='top -U $USERNAME'
alias unnethack='telnet eu.un.nethack.nu'
alias vi=vim
alias view='vim -R'

function vim {
    if (( ! ARGC )) set .
    command vim $@
}

bindkey '\eq' push-line-or-edit

bindkey '^XN' history-beginning-search-forward-end
bindkey '^XP' history-beginning-search-backward-end

bindkey '^Xn' history-beginning-search-forward
bindkey '^Xp' history-beginning-search-backward

bindkey -M menuselect h vi-backward-char
bindkey -M menuselect j vi-down-line-or-history
bindkey -M menuselect k vi-up-line-or-history
bindkey -M menuselect l vi-forward-char

function __prompt_hostname {
    REPLY=${${${+STY}#1}/0/${SSH_CONNECTION+%m }}
}

function __prompt_kerl {
    if (( ${+commands[kerl]} )) REPLY=$(kerl prompt '<%s> ')
}

function __prompt_virtual_env {
    REPLY=${VIRTUAL_ENV+(${VIRTUAL_ENV:t}) }
}

grml_theme_add_token cwd '%1~ ' '%B%F{blue}' %f%b
grml_theme_add_token hostname -i __prompt_hostname
grml_theme_add_token kerl -f __prompt_kerl %B %b
grml_theme_add_token virtual-env -f __prompt_virtual_env %B %b

zstyle :prompt:grml:left:setup items \
    rc kerl virtual-env hostname cwd vcs percent

zstyle :prompt:grml:right:setup use-rprompt no

zstyle ':vcs_info:*' enable git hg svn

zstyle ':vcs_info:*' actionformats \
    '%F{magenta}[%F{green}%b%F{yellow}|%F{red}%a%F{magenta}]%f ' '%s:%r'

zstyle ':vcs_info:*' formats \
    '%F{magenta}[%F{green}%b%F{magenta}]%f ' '%s:%r'
